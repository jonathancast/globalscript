	.prefix

	.data

gsdl.cgs.typeenv.gskind.get	.closure	_gsdl.cgs.typeenv.gskind.get_c

gsdl.cgs.typeenv.typeexpansion.get	.closure	_gsdl.cgs.typeenv.typeexpansion.get_c

gsdl.cgs.typeenv.typedefinition.get	.closure	_gsdl.cgs.typeenv.typedefinition.get_c

gsdl.cgs.typeenv.tyvar.elem	.closure	_gsdl.cgs.typeenv.tyvar.elem_c

gsdl.cgs.typeenv.empty	.closure	_gsdl.cgs.typeenv.empty_c

gsdl.cgs.typeenv.gsabstypenm.insert	.closure	_gsdl.cgs.typeenv.gsabstypenm.insert_c
gsdl.cgs.typeenv.definedprim.insert	.closure	_gsdl.cgs.typeenv.definedprim.insert_c
gsdl.cgs.typeenv.elimprim.insert	.closure	_gsdl.cgs.typeenv.elimprim.insert_c
gsdl.cgs.typeenv.impprim.insert	.closure	_gsdl.cgs.typeenv.impprim.insert_c
gsdl.cgs.typeenv.tyvar.insert	.closure	_gsdl.cgs.typeenv.tyvar.insert_c
gsdl.cgs.typeenv.typefun.insert	.closure	_gsdl.cgs.typeenv.typefun.insert_c

gsdl.cgs.typeenv.gsabstypedefn.insert	.closure	_gsdl.cgs.typeenv.gsabstypedefn.insert_c

gsdl.cgs.typeenv.var.insert	.closure	_gsdl.cgs.typeenv.var.insert_c

gsdl.cgs.typeenv.overlay	.closure	_gsdl.cgs.typeenv.overlay_c

gsdl.cgs.typeenv.typeexpansion.app	.closure	_gsdl.cgs.typeenv.typeexpansion.app_c

gsdl.cgs.typeenv.typeexpansion.substty	.closure	_gsdl.cgs.typeenv.typeexpansion.substty_c	_gsdl.cgs.typeenv.typeexpansion.substty_ty

gsdl.cgs.typeenv.typeexpansion.togstype	.closure	_gsdl.cgs.typeenv.typeexpansion.togstype_c

	.code

# §section §gs{gsdl.cgs.typeenv.gskind.get}

_gsdl.cgs.typeenv.gskind.get_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.gskind.get_k	.subcode
v	.larg	gsdl.var.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.gskind.get_k
	.enter	env

_gsdl.cgs.typeenv.gskind.get_k	.forcecont
gsdl.cgs.typeenv.u	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
ordered.map.get	.gvar
gsdl.var.compare	.gvar
v	.fv	gsdl.var.t
env0	.karg	gsdl.cgs.typeenv.u
kis	.field	kinds	env0
	.app	gsdl.var.compare	v	kis
	.enter	ordered.map.get	gsdl.var.t	gsdl.cgs.gskind.t

# §section §gs{gsdl.cgs.typeenv.typeexpansion.get}

_gsdl.cgs.typeenv.typeexpansion.get_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.get_k	.subcode
v	.larg	gsdl.var.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.typeexpansion.get_k
	.enter	env

_gsdl.cgs.typeenv.typeexpansion.get_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
ordered.map.get	.gvar
gsdl.var.compare	.gvar
v	.fv	gsdl.var.t
env0	.karg	gsdl.cgs.typeenv.u
exps	.field	typeexpansions	env0
	.app	gsdl.var.compare	v	exps
	.enter	ordered.map.get	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t

# §section §gs{gsdl.cgs.typeenv.typedefinition.get}

_gsdl.cgs.typeenv.typedefinition.get_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.typedefinition.get_k	.subcode
v	.larg	gsdl.var.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.typedefinition.get_k
	.enter	env

_gsdl.cgs.typeenv.typedefinition.get_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
ordered.map.get	.gvar
gsdl.var.compare	.gvar
v	.fv	gsdl.var.t
env0	.karg	gsdl.cgs.typeenv.u
tyds	.field	typedefinitions	env0
	.app	gsdl.var.compare	v	tyds
	.enter	ordered.map.get	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t

# §section §gs{gsdl.cgs.typeenv.tyvar.elem}

_gsdl.cgs.typeenv.tyvar.elem_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
bool.t	.tygvar	# Temp
ordered.map.elem	.gvar
gsdl.var.compare	.gvar
v	.larg	gsdl.var.t
env	.larg	gsdl.cgs.typeenv.t
kis	.lfield	kinds	env
	.app	gsdl.var.compare	v	kis
	.enter	ordered.map.elem	gsdl.var.t	gsdl.cgs.gskind.t

# §section §gs{gsdl.cgs.typeenv.empty}

_gsdl.cgs.typeenv.empty_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
ordered.map.empty	.gvar
kis	.apply	ordered.map.empty	gsdl.var.t	gsdl.cgs.gskind.t
es	.apply	ordered.map.empty	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t
tyds	.apply	ordered.map.empty	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t
r	.record	kinds	kis	typedefinitions	tyds	typeexpansions	es	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.gsabstypenm.insert}

_gsdl.cgs.typeenv.gsabstypenm.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.gsabstypenm.insert_k	.subcode
v	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.gsabstypenm.insert_k
	.enter	env

_gsdl.cgs.typeenv.gsabstypenm.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
list.nil	.gvar
gsdl.cgs.gstype.abstype	.gvar
v	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
e.ls	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e.ty	.apply	gsdl.cgs.gstype.abstype	|	v
e	.lrecord	0	e.ls	1	e.ty	|	gsdl.cgs.typeenv.typeexpansion.t
es0	.field	typeexpansions	env0
es1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	es0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	es1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.definedprim.insert}

_gsdl.cgs.typeenv.definedprim.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.definedprim.insert_k	.subcode
v	.larg	gsdl.var.t
ps	.larg	gsdl.var.t
pn	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.definedprim.insert_k
	.enter	env

_gsdl.cgs.typeenv.definedprim.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
list.nil	.gvar
gsdl.cgs.gstype.definedprim	.gvar
v	.fv	gsdl.var.t
pn	.fv	gsdl.var.t
ps	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
nil	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
ety	.apply	gsdl.cgs.gstype.definedprim	|	ps	pn	ki
e	.lrecord	0	nil	1	ety	|	gsdl.cgs.typeenv.typeexpansion.t
es0	.field	typeexpansions	env0
es1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	es0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	es1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.elimprim.insert}

_gsdl.cgs.typeenv.elimprim.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.elimprim.insert_k	.subcode
v	.larg	gsdl.var.t
ps	.larg	gsdl.var.t
pn	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.elimprim.insert_k
	.enter	env

_gsdl.cgs.typeenv.elimprim.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
list.nil	.gvar
gsdl.cgs.gstype.elimprim	.gvar
v	.fv	gsdl.var.t
ps	.fv	gsdl.var.t
pn	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
nil	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
ety	.apply	gsdl.cgs.gstype.elimprim	|	ps	pn	ki
e	.lrecord	0	nil	1	ety	|	gsdl.cgs.typeenv.typeexpansion.t
es0	.field	typeexpansions	env0
es1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	es0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	es1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.impprim.insert}

_gsdl.cgs.typeenv.impprim.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.impprim.insert_k	.subcode
v	.larg	gsdl.var.t
ps	.larg	gsdl.var.t
pn	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.impprim.insert_k
	.enter	env

_gsdl.cgs.typeenv.impprim.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
list.nil	.gvar
gsdl.cgs.gstype.impprim	.gvar
v	.fv	gsdl.var.t
ps	.fv	gsdl.var.t
pn	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
nil	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e.ty	.apply	gsdl.cgs.gstype.impprim	|	ps	pn	ki
e	.lrecord	0	nil	1	e.ty	|	gsdl.cgs.typeenv.typeexpansion.t
es0	.field	typeexpansions	env0
es1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	es0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	es1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.tyvar.insert}

_gsdl.cgs.typeenv.tyvar.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.tyvar.insert_k	.subcode
v	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.tyvar.insert_k
	.enter	env

_gsdl.cgs.typeenv.tyvar.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
list.nil	.gvar
gsdl.cgs.gstype.var	.gvar
v	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
nil	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e.ty	.apply	gsdl.cgs.gstype.var	|	v
e	.lrecord	0	nil	1	e.ty	|	gsdl.cgs.typeenv.typeexpansion.t
tyes0	.field	typeexpansions	env0
tyes1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	tyes0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	tyes1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.typefun.insert}

_gsdl.cgs.typeenv.typefun.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.typefun.insert_k	.subcode
v	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
as	.larg	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e.ty	.larg	gsdl.cgs.gstype.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.typefun.insert_k
	.enter	env

_gsdl.cgs.typeenv.typefun.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
list.nil	.gvar
gsdl.cgs.gstype.var	.gvar
v	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
as	.fv	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e.ty	.fv	gsdl.cgs.gstype.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
e	.lrecord	0	as	1	e.ty	|	gsdl.cgs.typeenv.typeexpansion.t
tyes0	.field	typeexpansions	env0
tyes1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	tyes0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	tyes1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.gsabstypedefn.insert}

_gsdl.cgs.typeenv.gsabstypedefn.insert_c	.expr
gsdl.var.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.gsabstypedefn.insert_k	.subcode
v	.larg	gsdl.var.t
as	.larg	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e	.larg	gsdl.cgs.gstype.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.gsabstypedefn.insert_k
	.enter	env

_gsdl.cgs.typeenv.gsabstypedefn.insert_k	.forcecont
gsdl.var.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
v	.fv	gsdl.var.t
as	.fv	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e	.fv	gsdl.cgs.gstype.t
env0	.karg	gsdl.cgs.typeenv.u
kis	.field	kinds	env0
dfn	.lrecord	tyargs	as	tybody	e	|	gsdl.cgs.typeenv.typedefinition.t
tyds	.field	typedefinitions	env0
tyds1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t	|	gsdl.var.compare	v	dfn	tyds
tyes	.field	typeexpansions	env0
r	.record	kinds	kis	typedefinitions	tyds1	typeexpansions	tyes	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.var.insert}

_gsdl.cgs.typeenv.var.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.var.insert_k	.subcode
v	.larg	gsdl.var.t
ty	.larg	gsdl.cgs.gstype.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.var.insert_k
	.enter	env

_gsdl.cgs.typeenv.var.insert_k	.forcecont
gsdl.cgs.typeenv.u	.tygvar
env0	.karg	gsdl.cgs.typeenv.u
kis	.field	kinds	env0
tyds	.field	typedefinitions	env0
tyes	.field	typeexpansions	env0
r	.record	kinds	kis	typedefinitions	tyds	typeexpansions	tyes	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.overlay}

_gsdl.cgs.typeenv.overlay_c	.expr
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.overlay_k0	.subcode
env0	.larg	gsdl.cgs.typeenv.t
env1	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.overlay_k0
	.enter	env0

_gsdl.cgs.typeenv.overlay_k0	.forcecont
gsdl.cgs.typeenv.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
_gsdl.cgs.typeenv.overlay_k1	.subcode
env1	.fv	gsdl.cgs.typeenv.t
env0.0	.karg	gsdl.cgs.typeenv.u
	.force	_gsdl.cgs.typeenv.overlay_k1
	.enter	env1

_gsdl.cgs.typeenv.overlay_k1	.forcecont
gsdl.cgs.typeenv.u	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
ordered.map.∪	.gvar
gsdl.var.compare	.gvar
env0.0	.fv	gsdl.cgs.typeenv.u
env1.0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0.0
kis1	.field	kinds	env1.0
kis2	.apply	ordered.map.∪	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	kis0	kis1
tyds0	.field	typedefinitions	env0.0
tyds1	.field	typedefinitions	env1.0
tyds2	.apply	ordered.map.∪	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t	|	gsdl.var.compare	tyds0	tyds1
es0	.field	typeexpansions	env0.0
es1	.field	typeexpansions	env1.0
es2	.apply	ordered.map.∪	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	es0	es1
r	.record	kinds	kis2	typedefinitions	tyds2	typeexpansions	es2	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.typeexpansion.app}

_gsdl.cgs.typeenv.typeexpansion.app_c	.expr
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.app_k	.subcode
tyf	.larg	gsdl.cgs.typeenv.typeexpansion.t
tya	.larg	gsdl.cgs.gstype.t
	.force	_gsdl.cgs.typeenv.typeexpansion.app_k
	.enter	tyf

_gsdl.cgs.typeenv.typeexpansion.app_k	.forcecont
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.app_k1	.subcode
list.t.out	.cogvar
tya	.fv	gsdl.cgs.gstype.t
tyf0	.karg	gsdl.cgs.typeenv.typeexpansion.prim.u
tyls	.field	0	tyf0
e	.field	1	tyf0
	.force	_gsdl.cgs.typeenv.typeexpansion.app_k1
	.coerce	list.t.out	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.enter	tyls

_gsdl.cgs.typeenv.typeexpansion.app_k1	.forcecont
gsdl.cgs.gstype.t	.tygvar
list.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.substty	.gvar
list.nil	.gvar
gsdl.cgs.gstype.app	.gvar
tya	.fv	gsdl.cgs.gstype.t
e	.fv	gsdl.cgs.gstype.t
tyls0	.karg	list.prim.u	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.analyze	tyls0	:	nil
		.case	:
l0		.fkarg	0	gsdl.cgs.typeenv.typeexpansion.tylambda.t
ls1		.fkarg	1	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
v			.lfield	0	l0
etyf1			.lrecord	0	ls1	1	e	|	gsdl.cgs.typeenv.typeexpansion.t
			.app	v	tya	etyf1
			.enter	gsdl.cgs.typeenv.typeexpansion.substty
		.case	nil
ls1			.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e1			.apply	gsdl.cgs.gstype.app	|	e	tya
r			.record	0	ls1	1	e1	|	gsdl.cgs.typeenv.typeexpansion.prim.u
			.lift
			.yield	r

# §section §gs{gsdl.cgs.typeenv.typeexpansion.substty}

_gsdl.cgs.typeenv.typeexpansion.substty_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.substty_k	.subcode
v	.larg	gsdl.var.t
ty	.larg	gsdl.cgs.gstype.t
tye	.larg	gsdl.cgs.typeenv.typeexpansion.t
	.force	_gsdl.cgs.typeenv.typeexpansion.substty_k
	.enter	tye

_gsdl.cgs.typeenv.typeexpansion.substty_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.substty_k1	.subcode
list.t.out	.cogvar
v	.fv	gsdl.var.t
ty	.fv	gsdl.cgs.gstype.t
tye0	.karg	gsdl.cgs.typeenv.typeexpansion.prim.u
tyls	.field	0	tye0
e	.field	1	tye0
	.force	_gsdl.cgs.typeenv.typeexpansion.substty_k1
	.coerce	list.t.out	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.enter	tyls

_gsdl.cgs.typeenv.typeexpansion.substty_k1	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
list.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar	# Temp
list.nil	.gvar
gsdl.cgs.gstype.substty	.gvar
v	.fv	gsdl.var.t
ty	.fv	gsdl.cgs.gstype.t
e	.fv	gsdl.cgs.gstype.t
tyls0	.karg	list.prim.u	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.analyze	tyls0	:	nil
		.case	:
tyl0		.fkarg	0	gsdl.cgs.typeenv.typeexpansion.tylambda.t
tyls1		.fkarg	1	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
			.undef	gsdl.cgs.typeenv.typeexpansion.t
		.case	nil
nil			.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e1			.apply	gsdl.cgs.gstype.substty	|	v	ty	e
r			.record	0	nil	1	e1	|	gsdl.cgs.typeenv.typeexpansion.prim.u
			.lift
			.yield	r

# §section §gs{gsdl.cgs.typeenv.typeexpansion.togstype}

_gsdl.cgs.typeenv.typeexpansion.togstype_c	.expr
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.togstype_k	.subcode
tye	.larg	gsdl.cgs.typeenv.typeexpansion.t
	.force	_gsdl.cgs.typeenv.typeexpansion.togstype_k
	.enter	tye

_gsdl.cgs.typeenv.typeexpansion.togstype_k	.forcecont
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.togstype_k1	.subcode
list.t.out	.cogvar
tye0	.karg	gsdl.cgs.typeenv.typeexpansion.prim.u
tyls	.field	0	tye0
e	.field	1	tye0
	.force	_gsdl.cgs.typeenv.typeexpansion.togstype_k1
	.coerce	list.t.out	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.enter	tyls

_gsdl.cgs.typeenv.typeexpansion.togstype_k1	.forcecont
gsdl.cgs.gstype.t	.tygvar
list.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
list.t	.tygvar
string.t	.tygvar
either.t	.tygvar	# Temp
either.right	.gvar
e	.fv	gsdl.cgs.gstype.t
tyls0	.karg	list.prim.u	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.analyze	tyls0	:	nil
		.case	:
tyl0		.fkarg	0	gsdl.cgs.typeenv.typeexpansion.tylambda.t
tyls1		.fkarg	1	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
			.undef	either.t	string.t	gsdl.cgs.gstype.t
		.case	nil
			.app	e
			.enter	either.right	string.t	gsdl.cgs.gstype.t

	.type

# §section §gs{gsdl.cgs.typeenv.t}

gsdl.cgs.typeenv.t	.tyexpr
gsdl.cgs.typeenv.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.typeenv.u

gsdl.cgs.typeenv.u	.tyexpr
ordered.map.t	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
kinds_ty	.tylet	ordered.map.t	gsdl.var.t	gsdl.cgs.gskind.t
_typedefinitions_ty	.tylet	ordered.map.t	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t
typeexpansions_ty	.tylet	ordered.map.t	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t
	.typroduct	kinds	kinds_ty	typedefinitions	_typedefinitions_ty	typeexpansions	typeexpansions_ty

# §section §gs{gsdl.cgs.typeenv.typeexpansion.t}

gsdl.cgs.typeenv.typeexpansion.t	.tyexpr	*
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.typeenv.typeexpansion.prim.u

gsdl.cgs.typeenv.typeexpansion.prim.u	.tyexpr
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
list.tylambda.ty	.tylet	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.typroduct	0	list.tylambda.ty	1	gsdl.cgs.gstype.t

# §section §gs{gsdl.cgs.typeenv.typeexpansion.tylambda.t}

gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tyexpr
gsdl.cgs.typeenv.typeexpansion.tylambda.prim.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.typeenv.typeexpansion.tylambda.prim.u

gsdl.cgs.typeenv.typeexpansion.tylambda.prim.u	.tyexpr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
	.typroduct	0	gsdl.var.t	1	gsdl.cgs.gskind.t

# §section §gs{gsdl.cgs.typeenv.typedefinition.t}

gsdl.cgs.typeenv.typedefinition.t	.tyexpr
gsdl.cgs.typeenv.typedefinition.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.typeenv.typedefinition.u

gsdl.cgs.typeenv.typedefinition.u	.tyexpr
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
list.tylambda.ty	.tylet	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.typroduct	tyargs	list.tylambda.ty	tybody	gsdl.cgs.gstype.t

# §section §gs{gsdl.cgs.typeenv.typeexpansion.substty}

_gsdl.cgs.typeenv.typeexpansion.substty_ty	.tyexpr
gsdl.var.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
	.tylift
	.tyfun	gsdl.var.t
	.tylift
	.tyfun	gsdl.cgs.gstype.t
	.tylift
	.tyfun	gsdl.cgs.typeenv.typeexpansion.t
	.tyref	gsdl.cgs.typeenv.typeexpansion.t
