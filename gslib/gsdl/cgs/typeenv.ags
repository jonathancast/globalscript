	.prefix

	.data

gsdl.cgs.typeenv.gskind.get	.closure	_gsdl.cgs.typeenv.gskind.get_c

gsdl.cgs.typeenv.typeexpansion.get	.closure	_gsdl.cgs.typeenv.typeexpansion.get_c

gsdl.cgs.typeenv.typedefinition.get	.closure	_gsdl.cgs.typeenv.typedefinition.get_c

gsdl.cgs.typeenv.tyvar.elem	.closure	_gsdl.cgs.typeenv.tyvar.elem_c

gsdl.cgs.typeenv.empty	.closure	_gsdl.cgs.typeenv.empty_c

gsdl.cgs.typeenv.gsabstypenm.insert	.closure	_gsdl.cgs.typeenv.gsabstypenm.insert_c
gsdl.cgs.typeenv.definedprim.insert	.closure	_gsdl.cgs.typeenv.definedprim.insert_c
gsdl.cgs.typeenv.elimprim.insert	.closure	_gsdl.cgs.typeenv.elimprim.insert_c
gsdl.cgs.typeenv.impprim.insert	.closure	_gsdl.cgs.typeenv.impprim.insert_c
gsdl.cgs.typeenv.tyvar.insert	.closure	_gsdl.cgs.typeenv.tyvar.insert_c

gsdl.cgs.typeenv.gsabstypedefn.insert	.closure	_gsdl.cgs.typeenv.gsabstypedefn.insert_c

gsdl.cgs.typeenv.var.insert	.closure	_gsdl.cgs.typeenv.var.insert_c

gsdl.cgs.typeenv.overlay	.closure	_gsdl.cgs.typeenv.overlay_c

gsdl.cgs.typeenv.typeexpansion.app	.closure	_gsdl.cgs.typeenv.typeexpansion.app_c

gsdl.cgs.typeenv.typeexpansion.togstype	.closure	_gsdl.cgs.typeenv.typeexpansion.togstype_c

gsdl.cgs.typeenv.typeexpansion.definedprim	.closure	_gsdl.cgs.typeenv.typeexpansion.definedprim_c
gsdl.cgs.typeenv.typeexpansion.expr	.closure	_gsdl.cgs.typeenv.typeexpansion.expr_c

	.code

# §section §gs{gsdl.cgs.typeenv.gskind.get}

_gsdl.cgs.typeenv.gskind.get_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.gskind.get_k	.subcode
v	.larg	gsdl.var.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.gskind.get_k
	.enter	env

_gsdl.cgs.typeenv.gskind.get_k	.forcecont
gsdl.cgs.typeenv.u	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
ordered.map.get	.gvar
gsdl.var.compare	.gvar
v	.fv	gsdl.var.t
env0	.karg	gsdl.cgs.typeenv.u
kis	.field	kinds	env0
	.app	gsdl.var.compare	v	kis
	.enter	ordered.map.get	gsdl.var.t	gsdl.cgs.gskind.t

# §section §gs{gsdl.cgs.typeenv.typeexpansion.get}

_gsdl.cgs.typeenv.typeexpansion.get_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.get_k	.subcode
v	.larg	gsdl.var.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.typeexpansion.get_k
	.enter	env

_gsdl.cgs.typeenv.typeexpansion.get_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
ordered.map.get	.gvar
gsdl.var.compare	.gvar
v	.fv	gsdl.var.t
env0	.karg	gsdl.cgs.typeenv.u
exps	.field	typeexpansions	env0
	.app	gsdl.var.compare	v	exps
	.enter	ordered.map.get	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t

# §section §gs{gsdl.cgs.typeenv.typedefinition.get}

_gsdl.cgs.typeenv.typedefinition.get_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.typedefinition.get_k	.subcode
v	.larg	gsdl.var.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.typedefinition.get_k
	.enter	env

_gsdl.cgs.typeenv.typedefinition.get_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
ordered.map.get	.gvar
gsdl.var.compare	.gvar
v	.fv	gsdl.var.t
env0	.karg	gsdl.cgs.typeenv.u
tyds	.field	typedefinitions	env0
	.app	gsdl.var.compare	v	tyds
	.enter	ordered.map.get	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t

# §section §gs{gsdl.cgs.typeenv.tyvar.elem}

_gsdl.cgs.typeenv.tyvar.elem_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
bool.t	.tygvar	# Temp
ordered.map.elem	.gvar
gsdl.var.compare	.gvar
v	.larg	gsdl.var.t
env	.larg	gsdl.cgs.typeenv.t
kis	.lfield	kinds	env
	.app	gsdl.var.compare	v	kis
	.enter	ordered.map.elem	gsdl.var.t	gsdl.cgs.gskind.t

# §section §gs{gsdl.cgs.typeenv.empty}

_gsdl.cgs.typeenv.empty_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
ordered.map.empty	.gvar
kis	.apply	ordered.map.empty	gsdl.var.t	gsdl.cgs.gskind.t
es	.apply	ordered.map.empty	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t
tyds	.apply	ordered.map.empty	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t
r	.record	kinds	kis	typedefinitions	tyds	typeexpansions	es	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.gsabstypenm.insert}

_gsdl.cgs.typeenv.gsabstypenm.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.gsabstypenm.insert_k	.subcode
v	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.gsabstypenm.insert_k
	.enter	env

_gsdl.cgs.typeenv.gsabstypenm.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
gsdl.cgs.typeenv.typeexpansion.expr	.gvar
list.nil	.gvar
gsdl.cgs.gstype.abstype	.gvar
v	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
e.ls	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e.ty	.apply	gsdl.cgs.gstype.abstype	|	v
e	.apply	gsdl.cgs.typeenv.typeexpansion.expr	|	e.ls	e.ty
es0	.field	typeexpansions	env0
es1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	es0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	es1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.definedprim.insert}

_gsdl.cgs.typeenv.definedprim.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.definedprim.insert_k	.subcode
v	.larg	gsdl.var.t
ps	.larg	gsdl.var.t
pn	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.definedprim.insert_k
	.enter	env

_gsdl.cgs.typeenv.definedprim.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
gsdl.cgs.typeenv.typeexpansion.definedprim	.gvar
v	.fv	gsdl.var.t
pn	.fv	gsdl.var.t
ps	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
e	.apply	gsdl.cgs.typeenv.typeexpansion.definedprim	|	pn	ps	ki
es0	.field	typeexpansions	env0
es1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	es0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	es1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.elimprim.insert}

_gsdl.cgs.typeenv.elimprim.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.elimprim.insert_k	.subcode
v	.larg	gsdl.var.t
ps	.larg	gsdl.var.t
pn	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.elimprim.insert_k
	.enter	env

_gsdl.cgs.typeenv.elimprim.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
gsdl.cgs.typeenv.typeexpansion.expr	.gvar
list.nil	.gvar
gsdl.cgs.gstype.elimprim	.gvar
v	.fv	gsdl.var.t
ps	.fv	gsdl.var.t
pn	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
nil	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
ety	.apply	gsdl.cgs.gstype.elimprim	|	ps	pn	ki
e	.apply	gsdl.cgs.typeenv.typeexpansion.expr	|	nil	ety
es0	.field	typeexpansions	env0
es1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	es0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	es1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.impprim.insert}

_gsdl.cgs.typeenv.impprim.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.impprim.insert_k	.subcode
v	.larg	gsdl.var.t
ps	.larg	gsdl.var.t
pn	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.impprim.insert_k
	.enter	env

_gsdl.cgs.typeenv.impprim.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
gsdl.cgs.typeenv.typeexpansion.expr	.gvar
list.nil	.gvar
gsdl.cgs.gstype.impprim	.gvar
v	.fv	gsdl.var.t
ps	.fv	gsdl.var.t
pn	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
nil	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e.ty	.apply	gsdl.cgs.gstype.impprim	|	ps	pn	ki
e	.apply	gsdl.cgs.typeenv.typeexpansion.expr	|	nil	e.ty
es0	.field	typeexpansions	env0
es1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	es0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	es1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.tyvar.insert}

_gsdl.cgs.typeenv.tyvar.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.tyvar.insert_k	.subcode
v	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.tyvar.insert_k
	.enter	env

_gsdl.cgs.typeenv.tyvar.insert_k	.forcecont
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
gsdl.cgs.typeenv.typeexpansion.expr	.gvar
list.nil	.gvar
gsdl.cgs.gstype.var	.gvar
v	.fv	gsdl.var.t
ki	.fv	gsdl.cgs.gskind.t
env0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0
kis1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	v	ki	kis0
tyds	.field	typedefinitions	env0
nil	.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e.ty	.apply	gsdl.cgs.gstype.var	|	v
e	.apply	gsdl.cgs.typeenv.typeexpansion.expr	|	nil	e.ty
tyes0	.field	typeexpansions	env0
tyes1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	v	e	tyes0
r	.record	kinds	kis1	typedefinitions	tyds	typeexpansions	tyes1	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.gsabstypedefn.insert}

_gsdl.cgs.typeenv.gsabstypedefn.insert_c	.expr
gsdl.var.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.gsabstypedefn.insert_k	.subcode
v	.larg	gsdl.var.t
as	.larg	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e	.larg	gsdl.cgs.gstype.t
env	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.gsabstypedefn.insert_k
	.enter	env

_gsdl.cgs.typeenv.gsabstypedefn.insert_k	.forcecont
gsdl.var.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
ordered.map.insert	.gvar
gsdl.var.compare	.gvar
v	.fv	gsdl.var.t
as	.fv	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e	.fv	gsdl.cgs.gstype.t
env0	.karg	gsdl.cgs.typeenv.u
kis	.field	kinds	env0
dfn	.lrecord	tyargs	as	tybody	e	|	gsdl.cgs.typeenv.typedefinition.t
tyds	.field	typedefinitions	env0
tyds1	.apply	ordered.map.insert	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t	|	gsdl.var.compare	v	dfn	tyds
tyes	.field	typeexpansions	env0
r	.record	kinds	kis	typedefinitions	tyds1	typeexpansions	tyes	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.var.insert}

_gsdl.cgs.typeenv.var.insert_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.t	.tygvar
v	.larg	gsdl.var.t
ty	.larg	gsdl.cgs.gstype.t
ki	.larg	gsdl.cgs.gskind.t
env	.larg	gsdl.cgs.typeenv.t
	.undef	gsdl.cgs.typeenv.t

# §section §gs{gsdl.cgs.typeenv.overlay}

_gsdl.cgs.typeenv.overlay_c	.expr
gsdl.cgs.typeenv.t	.tygvar
_gsdl.cgs.typeenv.overlay_k0	.subcode
env0	.larg	gsdl.cgs.typeenv.t
env1	.larg	gsdl.cgs.typeenv.t
	.force	_gsdl.cgs.typeenv.overlay_k0
	.enter	env0

_gsdl.cgs.typeenv.overlay_k0	.forcecont
gsdl.cgs.typeenv.t	.tygvar
gsdl.cgs.typeenv.u	.tygvar
_gsdl.cgs.typeenv.overlay_k1	.subcode
env1	.fv	gsdl.cgs.typeenv.t
env0.0	.karg	gsdl.cgs.typeenv.u
	.force	_gsdl.cgs.typeenv.overlay_k1
	.enter	env1

_gsdl.cgs.typeenv.overlay_k1	.forcecont
gsdl.cgs.typeenv.u	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
ordered.map.∪	.gvar
gsdl.var.compare	.gvar
env0.0	.fv	gsdl.cgs.typeenv.u
env1.0	.karg	gsdl.cgs.typeenv.u
kis0	.field	kinds	env0.0
kis1	.field	kinds	env1.0
kis2	.apply	ordered.map.∪	gsdl.var.t	gsdl.cgs.gskind.t	|	gsdl.var.compare	kis0	kis1
tyds0	.field	typedefinitions	env0.0
tyds1	.field	typedefinitions	env1.0
tyds2	.apply	ordered.map.∪	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t	|	gsdl.var.compare	tyds0	tyds1
es0	.field	typeexpansions	env0.0
es1	.field	typeexpansions	env1.0
es2	.apply	ordered.map.∪	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t	|	gsdl.var.compare	es0	es1
r	.record	kinds	kis2	typedefinitions	tyds2	typeexpansions	es2	|	gsdl.cgs.typeenv.u
	.lift
	.yield	r

# §section §gs{gsdl.cgs.typeenv.typeexpansion.app}

_gsdl.cgs.typeenv.typeexpansion.app_c	.expr
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.app_k	.subcode
gsdl.cgs.typeenv.typeexpansion.t.out	.cogvar
tyf	.larg	gsdl.cgs.typeenv.typeexpansion.t
tya	.larg	gsdl.cgs.gstype.t
	.force	_gsdl.cgs.typeenv.typeexpansion.app_k
	.coerce	gsdl.cgs.typeenv.typeexpansion.t.out
	.enter	tyf

_gsdl.cgs.typeenv.typeexpansion.app_k	.forcecont
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar	# Temp
_gsdl.cgs.typeenv.typeexpansion.app_expr_k	.subcode
list.t.out	.cogvar
gsdl.cgs.typeenv.typeexpansion.expr	.gvar
list.nil	.gvar
gsdl.cgs.gstype.app	.gvar
gsdl.cgs.gstype.abstype	.gvar
tya	.fv	gsdl.cgs.gstype.t
tyf0	.karg	gsdl.cgs.typeenv.typeexpansion.prim.u
	.analyze	tyf0	definedprim	expr
		.case	definedprim
ps		.fkarg	0	gsdl.var.t
pn		.fkarg	1	gsdl.var.t
ki		.fkarg	2	gsdl.cgs.gskind.t
			.undef	gsdl.cgs.typeenv.typeexpansion.t
		.case	expr
tyls		.fkarg	0	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e		.fkarg	1	gsdl.cgs.gstype.t
			.force	_gsdl.cgs.typeenv.typeexpansion.app_expr_k
			.coerce	list.t.out	gsdl.cgs.typeenv.typeexpansion.tylambda.t
			.enter	tyls
	# > :: gsdl.cgs.typeenv.typeexpansion.t

_gsdl.cgs.typeenv.typeexpansion.app_expr_k	.forcecont
gsdl.cgs.gstype.t	.tygvar
list.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar	# Temp
gsdl.cgs.typeenv.typeexpansion.expr	.gvar
list.nil	.gvar
gsdl.cgs.gstype.app	.gvar
tya	.fv	gsdl.cgs.gstype.t
e	.fv	gsdl.cgs.gstype.t
tyls0	.karg	list.prim.u	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.analyze	tyls0	:	nil
		.case	:
l0		.fkarg	0	gsdl.cgs.typeenv.typeexpansion.tylambda.t
ls1		.fkarg	1	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
			.undef	gsdl.cgs.typeenv.typeexpansion.t
		.case	nil
ls1			.apply	list.nil	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e1			.apply	gsdl.cgs.gstype.app	|	e	tya
			.app	ls1	e1
			.enter	gsdl.cgs.typeenv.typeexpansion.expr

# §section §gs{gsdl.cgs.typeenv.typeexpansion.togstype}

_gsdl.cgs.typeenv.typeexpansion.togstype_c	.expr
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
_gsdl.cgs.typeenv.typeexpansion.togstype_k	.subcode
gsdl.cgs.typeenv.typeexpansion.t.out	.cogvar
tye	.larg	gsdl.cgs.typeenv.typeexpansion.t
	.force	_gsdl.cgs.typeenv.typeexpansion.togstype_k
	.coerce	gsdl.cgs.typeenv.typeexpansion.t.out
	.enter	tye

_gsdl.cgs.typeenv.typeexpansion.togstype_k	.forcecont
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
string.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
either.t	.tygvar	# Temp
_gsdl.cgs.typeenv.typeexpansion.togstype_expr_k	.subcode
list.t.out	.cogvar
either.right	.gvar
gsdl.cgs.gstype.abstype	.gvar
tye0	.karg	gsdl.cgs.typeenv.typeexpansion.prim.u
	.analyze	tye0	definedprim	expr
		.case	definedprim
ps		.fkarg	0	gsdl.var.t
pn		.fkarg	1	gsdl.var.t
ki		.fkarg	2	gsdl.cgs.gskind.t
tyres			.undefined	gsdl.cgs.gstype.t
			.app	tyres
			.enter	either.right	string.t	gsdl.cgs.gstype.t
		.case	expr
tyls		.fkarg	0	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e		.fkarg	1	gsdl.cgs.gstype.t
			.force	_gsdl.cgs.typeenv.typeexpansion.togstype_expr_k
			.coerce	list.t.out	gsdl.cgs.typeenv.typeexpansion.tylambda.t
			.enter	tyls
	# > :: either.t	string.t	gsdl.cgs.gstype.t

_gsdl.cgs.typeenv.typeexpansion.togstype_expr_k	.forcecont
gsdl.cgs.gstype.t	.tygvar
list.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
list.t	.tygvar
string.t	.tygvar
either.t	.tygvar	# Temp
either.right	.gvar
e	.fv	gsdl.cgs.gstype.t
tyls0	.karg	list.prim.u	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.analyze	tyls0	:	nil
		.case	:
tyl0		.fkarg	0	gsdl.cgs.typeenv.typeexpansion.tylambda.t
tyls1		.fkarg	1	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
			.undef	either.t	string.t	gsdl.cgs.gstype.t
		.case	nil
			.app	e
			.enter	either.right	string.t	gsdl.cgs.gstype.t

# §section §gs{gsdl.cgs.typeenv.typeexpansion.t} Constructors

_gsdl.cgs.typeenv.typeexpansion.definedprim_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t.in	.cogvar
ps	.larg	gsdl.var.t
pn	.larg	gsdl.var.t
ki	.larg	gsdl.cgs.gskind.t
c	.constr	gsdl.cgs.typeenv.typeexpansion.prim.u	definedprim	0 ps	1	pn	2	ki
	.coerce	gsdl.cgs.typeenv.typeexpansion.t.in
	.lift
	.yield	c

_gsdl.cgs.typeenv.typeexpansion.expr_c	.expr
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
gsdl.cgs.typeenv.typeexpansion.t.in	.cogvar
tyls	.larg	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
e	.larg	gsdl.cgs.gstype.t
c	.constr	gsdl.cgs.typeenv.typeexpansion.prim.u	expr	0	tyls	1	e
	.coerce	gsdl.cgs.typeenv.typeexpansion.t.in
	.lift
	.yield	c

	.type

# §section §gs{gsdl.cgs.typeenv.t}

gsdl.cgs.typeenv.t	.tyexpr
gsdl.cgs.typeenv.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.typeenv.u

gsdl.cgs.typeenv.u	.tyexpr
ordered.map.t	.tygvar
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
gsdl.cgs.typeenv.typedefinition.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
kinds_ty	.tylet	ordered.map.t	gsdl.var.t	gsdl.cgs.gskind.t
_typedefinitions_ty	.tylet	ordered.map.t	gsdl.var.t	gsdl.cgs.typeenv.typedefinition.t
typeexpansions_ty	.tylet	ordered.map.t	gsdl.var.t	gsdl.cgs.typeenv.typeexpansion.t
	.typroduct	kinds	kinds_ty	typedefinitions	_typedefinitions_ty	typeexpansions	typeexpansions_ty

# §section §gs{gsdl.cgs.typeenv.typeexpansion.t}

gsdl.cgs.typeenv.typeexpansion.t	.tyabstract	*
gsdl.cgs.typeenv.typeexpansion.prim.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.typeenv.typeexpansion.prim.u

gsdl.cgs.typeenv.typeexpansion.prim.u	.tyexpr
_gsdl.cgs.typeenv.typeexpansion.prim.u_definedprim_ty	.tygvar
_gsdl.cgs.typeenv.typeexpansion.prim.u_expr_ty	.tygvar
	.tysum	definedprim	_gsdl.cgs.typeenv.typeexpansion.prim.u_definedprim_ty	expr	_gsdl.cgs.typeenv.typeexpansion.prim.u_expr_ty

_gsdl.cgs.typeenv.typeexpansion.prim.u_definedprim_ty	.tyexpr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
	.tyubproduct	0	gsdl.var.t	1	gsdl.var.t	2	gsdl.cgs.gskind.t

_gsdl.cgs.typeenv.typeexpansion.prim.u_expr_ty	.tyexpr
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
list.tylambda.ty	.tylet	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.tyubproduct	0	list.tylambda.ty	1	gsdl.cgs.gstype.t

# §section §gs{gsdl.cgs.typeenv.typeexpansion.tylambda.t}

gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tyexpr
gsdl.cgs.typeenv.typeexpansion.tylambda.prim.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.typeenv.typeexpansion.tylambda.prim.u

gsdl.cgs.typeenv.typeexpansion.tylambda.prim.u	.tyexpr
gsdl.var.t	.tygvar
gsdl.cgs.gskind.t	.tygvar
	.typroduct	1	gsdl.var.t	2	gsdl.cgs.gskind.t

# §section §gs{gsdl.cgs.typeenv.typedefinition.t}

gsdl.cgs.typeenv.typedefinition.t	.tyexpr
gsdl.cgs.typeenv.typedefinition.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.typeenv.typedefinition.u

gsdl.cgs.typeenv.typedefinition.u	.tyexpr
list.t	.tygvar
gsdl.cgs.typeenv.typeexpansion.tylambda.t	.tygvar
gsdl.cgs.gstype.t	.tygvar
list.tylambda.ty	.tylet	list.t	gsdl.cgs.typeenv.typeexpansion.tylambda.t
	.typroduct	tyargs	list.tylambda.ty	tybody	gsdl.cgs.gstype.t

	.coercion

gsdl.cgs.typeenv.typeexpansion.t.in	.tycoercion
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
	.tydefinition	gsdl.cgs.typeenv.typeexpansion.t

gsdl.cgs.typeenv.typeexpansion.t.out	.tycoercion
gsdl.cgs.typeenv.typeexpansion.t	.tygvar
	.tyinvert
	.tydefinition	gsdl.cgs.typeenv.typeexpansion.t
