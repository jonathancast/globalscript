#calculus: gsdl.string-code 0.5

	.prefix

	.data

gsdl.cgs.symbol.components.get	.closure	_gsdl.cgs.symbol.components.get_c

gsdl.cgs.symbol.compare	.closure	_gsdl.cgs.symbol.compare_c

gsdl.cgs.symbol.≡	.closure	_gsdl.cgs.symbol.≡_c

gsdl.cgs.symbol.fmtgs	.closure	_gsdl.cgs.symbol.fmtgs_c
_gsdl.cgs.symbol.fmtgs_tyvar.s	.string	tyvar

gsdl.cgs.symbol.var	.closure	_gsdl.cgs.symbol.var_c
gsdl.cgs.symbol.tyvar	.closure	_gsdl.cgs.symbol.tyvar_c
gsdl.cgs.symbol.abstyvar	.closure	_gsdl.cgs.symbol.abstyvar_c

	.code

-- §section §gs{gsdl.cgs.symbol.components.get}

_gsdl.cgs.symbol.components.get_c	.expr
gsdl.cgs.symbol.t	.tygvar
_gsdl.cgs.symbol.components.get_k	.subcode
gsdl.cgs.symbol.t.out	.cogvar
s	.larg	gsdl.cgs.symbol.t
	.force	_gsdl.cgs.symbol.components.get_k
	.coerce	gsdl.cgs.symbol.t.out
	.enter	s

_gsdl.cgs.symbol.components.get_k	.forcecont
gsdl.cgs.symbol.prim.u	.tygvar
gsdl.var.t	.tygvar
list.t	.tygvar	-- Temp
gsdl.var.component.t	.tygvar	-- Temp
gsdl.var.components.get	.gvar
s0	.karg	gsdl.cgs.symbol.prim.u
	.analyze	s0	abstyvar	tyvar	var
		.case	abstyvar
v		.fkarg	0	gsdl.var.t
			.undef	list.t	gsdl.var.component.t
		.case	tyvar
v		.fkarg	0	gsdl.var.t
			.app	v
			.enter	gsdl.var.components.get
		.case	var
v		.fkarg	0	gsdl.var.t
			.undef	list.t	gsdl.var.component.t
	-- > :: list.t	gsdl.var.component.t

-- §section §gs{gsdl.cgs.symbol.compare}

_gsdl.cgs.symbol.compare_c	.expr
gsdl.cgs.symbol.t	.tygvar
_gsdl.cgs.symbol.compare_k0	.subcode
gsdl.cgs.symbol.t.out	.cogvar
s0	.larg	gsdl.cgs.symbol.t
s1	.larg	gsdl.cgs.symbol.t
	.force	_gsdl.cgs.symbol.compare_k0
	.coerce	gsdl.cgs.symbol.t.out
	.enter	s0

_gsdl.cgs.symbol.compare_k0	.forcecont
gsdl.cgs.symbol.t	.tygvar
gsdl.cgs.symbol.prim.u	.tygvar
_gsdl.cgs.symbol.compare_k1	.subcode
gsdl.cgs.symbol.t.out	.cogvar
s1	.fv	gsdl.cgs.symbol.t
s0.0	.karg	gsdl.cgs.symbol.prim.u
	.force	_gsdl.cgs.symbol.compare_k1
	.coerce	gsdl.cgs.symbol.t.out
	.enter	s1

_gsdl.cgs.symbol.compare_k1	.forcecont
gsdl.cgs.symbol.prim.u	.tygvar
gsdl.var.t	.tygvar
order.total.ord.t	.tygvar	-- Temp
gsdl.var.compare	.gvar
order.total.ord.lt	.gvar
order.total.ord.gt	.gvar
s0.0	.fv	gsdl.cgs.symbol.prim.u
s1.0	.karg	gsdl.cgs.symbol.prim.u
	.analyze	s0.0	abstyvar	tyvar	var
		.case	abstyvar
v0		.fkarg	0	gsdl.var.t
			.analyze	s1.0	abstyvar	tyvar	var
				.case	abstyvar
v1				.fkarg	0	gsdl.var.t
					.app	v0	v1
					.enter	gsdl.var.compare
				.case	tyvar
v1				.fkarg	0	gsdl.var.t
					.enter	order.total.ord.lt
				.case	var
v1				.fkarg	0	gsdl.var.t
					.enter	order.total.ord.lt
		.case	tyvar
v0		.fkarg	0	gsdl.var.t
			.analyze	s1.0	abstyvar	tyvar	var
				.case	abstyvar
v1				.fkarg	0	gsdl.var.t
					.enter	order.total.ord.gt
				.case	tyvar
v1				.fkarg	0	gsdl.var.t
					.app	v0	v1
					.enter	gsdl.var.compare
				.case	var
v1				.fkarg	0	gsdl.var.t
					.undef	order.total.ord.t
		.case	var
v0		.fkarg	0	gsdl.var.t
			.analyze	s1.0	abstyvar	tyvar	var
				.case	abstyvar
v1				.fkarg	0	gsdl.var.t
					.undef	order.total.ord.t
				.case	tyvar
v1				.fkarg	0	gsdl.var.t
					.enter	order.total.ord.gt
				.case	var
v1				.fkarg	0	gsdl.var.t
					.undef	order.total.ord.t
	-- > :: order.total.ord.t

-- §section §gs{gsdl.cgs.symbol.≡}

_gsdl.cgs.symbol.≡_c	.expr
gsdl.cgs.symbol.t	.tygvar
_gsdl.cgs.symbol.≡_k0	.subcode
gsdl.cgs.symbol.t.out	.cogvar
s0	.larg	gsdl.cgs.symbol.t
s1	.larg	gsdl.cgs.symbol.t
	.force	_gsdl.cgs.symbol.≡_k0
	.coerce	gsdl.cgs.symbol.t.out
	.enter	s0

_gsdl.cgs.symbol.≡_k0	.forcecont
gsdl.cgs.symbol.t	.tygvar
gsdl.cgs.symbol.prim.u	.tygvar
_gsdl.cgs.symbol.≡_k1	.subcode
gsdl.cgs.symbol.t.out	.cogvar
s1	.fv	gsdl.cgs.symbol.t
s0.0	.karg	gsdl.cgs.symbol.prim.u
	.force	_gsdl.cgs.symbol.≡_k1
	.coerce	gsdl.cgs.symbol.t.out
	.enter	s1

_gsdl.cgs.symbol.≡_k1	.forcecont
gsdl.cgs.symbol.prim.u	.tygvar
gsdl.var.t	.tygvar
bool.t	.tygvar	-- Temp
gsdl.var.≡	.gvar
s0.0	.fv	gsdl.cgs.symbol.prim.u
s1.0	.karg	gsdl.cgs.symbol.prim.u
	.analyze	s0.0	abstyvar	tyvar	var
		.case	abstyvar
v0		.fkarg	0	gsdl.var.t
			.undef	bool.t
		.case	tyvar
v0		.fkarg	0	gsdl.var.t
			.danalyze	s1.0	tyvar
				.default
					.undef	bool.t
				.case	tyvar
v1				.fkarg	0	gsdl.var.t
					.app	v0	v1
					.enter	gsdl.var.≡
		.case	var
v0		.fkarg	0	gsdl.var.t
			.undef	bool.t
	-- > :: bool.t

-- §section §gs{gsdl.cgs.symbol.fmtgs}

_gsdl.cgs.symbol.fmtgs_c	.expr
gsdl.cgs.symbol.t	.tygvar
_gsdl.cgs.symbol.fmtgs_k	.subcode
gsdl.cgs.symbol.t.out	.cogvar
s	.larg	gsdl.cgs.symbol.t
	.force	_gsdl.cgs.symbol.fmtgs_k
	.coerce	gsdl.cgs.symbol.t.out
	.enter	s

_gsdl.cgs.symbol.fmtgs_k	.forcecont
gsdl.cgs.symbol.prim.u	.tygvar
gsdl.var.t	.tygvar
fmtgs.printer.t	.tygvar	-- Temp
fmtgs.app	.gvar
fmtgs.var	.gvar
_gsdl.cgs.symbol.fmtgs_tyvar.s	.gvar
gsdl.var.fmtgs	.gvar
s0	.karg	gsdl.cgs.symbol.prim.u
	.analyze	s0	abstyvar	tyvar	var
		.case	abstyvar
v		.fkarg	0	gsdl.var.t
			.undef	fmtgs.printer.t
		.case	tyvar
v		.fkarg	0	gsdl.var.t
tyfvar.fmt			.apply	fmtgs.var	|	_gsdl.cgs.symbol.fmtgs_tyvar.s
v.fmt			.apply	gsdl.var.fmtgs	|	v
			.app	tyfvar.fmt	v.fmt
			.enter	fmtgs.app
		.case	var
v		.fkarg	0	gsdl.var.t
			.undef	fmtgs.printer.t
	-- > :: fmtgs.printer.t

-- §section §gs{gsdl.cgs.symbol.t} Constructors

_gsdl.cgs.symbol.var_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.symbol.prim.u	.tygvar
gsdl.cgs.symbol.t.in	.cogvar
v	.arg	gsdl.var.t
c	.constr	gsdl.cgs.symbol.prim.u	var	0	v
	.coerce	gsdl.cgs.symbol.t.in
	.lift
	.yield	c

_gsdl.cgs.symbol.tyvar_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.symbol.prim.u	.tygvar
gsdl.cgs.symbol.t.in	.cogvar
v	.arg	gsdl.var.t
c	.constr	gsdl.cgs.symbol.prim.u	tyvar	0	v
	.coerce	gsdl.cgs.symbol.t.in
	.lift
	.yield	c

_gsdl.cgs.symbol.abstyvar_c	.expr
gsdl.var.t	.tygvar
gsdl.cgs.symbol.prim.u	.tygvar
gsdl.cgs.symbol.t.in	.cogvar
v	.arg	gsdl.var.t
c	.constr	gsdl.cgs.symbol.prim.u	abstyvar	0	v
	.coerce	gsdl.cgs.symbol.t.in
	.lift
	.yield	c

	.type

-- §section §gs{gsdl.cgs.symbol.t}

gsdl.cgs.symbol.t	.tyabstract	*
gsdl.cgs.symbol.prim.u	.tygvar
	.tylift
	.tyref	gsdl.cgs.symbol.prim.u

gsdl.cgs.symbol.prim.u	.tyexpr
_gsdl.cgs.symbol.prim.u_abstyvar_ty	.tygvar
_gsdl.cgs.symbol.prim.u_tyvar_ty	.tygvar
_gsdl.cgs.symbol.prim.u_var_ty	.tygvar
	.tysum	abstyvar	_gsdl.cgs.symbol.prim.u_abstyvar_ty	tyvar	_gsdl.cgs.symbol.prim.u_tyvar_ty	var	_gsdl.cgs.symbol.prim.u_var_ty

_gsdl.cgs.symbol.prim.u_abstyvar_ty	.tyexpr
gsdl.var.t	.tygvar
	.tyubproduct	0	gsdl.var.t

_gsdl.cgs.symbol.prim.u_tyvar_ty	.tyexpr
gsdl.var.t	.tygvar
	.tyubproduct	0	gsdl.var.t

_gsdl.cgs.symbol.prim.u_var_ty	.tyexpr
gsdl.var.t	.tygvar
	.tyubproduct	0	gsdl.var.t

	.coercion

gsdl.cgs.symbol.t.in	.tycoercion
gsdl.cgs.symbol.t	.tygvar
	.tydefinition	gsdl.cgs.symbol.t

gsdl.cgs.symbol.t.out	.tycoercion
gsdl.cgs.symbol.t	.tygvar
	.tyinvert
	.tydefinition	gsdl.cgs.symbol.t
