#calculus: gsdl.string-code 0.3

	.document

	.data

	.closure	_epcode

_tests	.list	test.property.t	_test.4.eq.4	_test.4.eq.0	_test.0.lt.4	_test.4.lt.0	_test.4.gt.0	_test.compare.4.4	_test_2.plus.3	_test.1.div.0	_test.2.div.3	_test.fmtdecimal.0	_test.fmtdecimal.11

_test.4.eq.4	.closure	_test.4.eq.4_c
_test.4.eq.4_label	.string	4\s≡\s4
_test.4.eq.4_test_fail	.string	4\s≠\s4
_test.4.eq.4_test_succ	.string	4\s≡\s4

_test.4.eq.0	.closure	_test.4.eq.0_c
_test.4.eq.0_label	.string	4\s≡\s0
_test.4.eq.0_test_succ	.string	4\s≠\s0
_test.4.eq.0_test_fail	.string	4\s≡\s0

_test.0.lt.4	.closure	_test.0.lt.4_c
_test.0.lt.4_label	.string	0\s<\s4
_test.0.lt.4_test	.closure	_test.0.lt.4_test_c
_test.0.lt.4_test_fail	.string	0\s≮\s4
_test.0.lt.4_test_succ	.string	0\s<\s4

_test.4.lt.0	.closure	_test.4.lt.0_c
_test.4.lt.0_label	.string	4\s<\s0
_test.4.lt.0_test	.closure	_test.4.lt.0_test_c
_test.4.lt.0_test_succ	.string	4\s≮\s0
_test.4.lt.0_test_fail	.string	4\s<\s0

_test.4.gt.0	.closure	_test.4.gt.0_c
_test.4.gt.0_label	.string	4\s>\s0
_test.4.gt.0_test	.closure	_test.4.gt.0_test_c
_test.4.gt.0_test_false	.string	4\s≯\s0
_test.4.gt.0_test_true	.string	4\s>\s0

_test.compare.4.4	.closure	_test.compare.4.4_c
_test.compare.4.4_label	.string	natural.compare\s4\s4

_test_2.plus.3	.closure	_test_2.plus.3_c
_test_2.plus.3_label	.string	2\s+\s3

_test.1.div.0	.closure	_test.1.div.0_c
_test.1.div.0_label	.string	1\s`divMod`\s0
_test.1.div.0_test	.closure	_test.1.div.0_test_c
_test.1.div.0_test_succ	.string	1\s`divMod`\s0\sreturns\sdivide\sby\s0
_test.1.div.0_test_fail	.string	1\s`divMod`\s0\sreturns\squotient\sand\sremainder

_test.2.div.3	.closure	_test.2.div.3_c
_test.2.div.3_label	.string	2\s`divMod`\s3
_test.2.div.3_test	.closure	_test.2.div.3_test_c
_test.2.div.3_test_quot_fail	.string	2\s`div`\s3≠0
_test.2.div.3_test_quot_succ	.string	2\s`div`\s3≡0
_test.2.div.3_test_mod_fail	.string	2\s`mod`\s3≠2
_test.2.div.3_test_mod_succ	.string	2\s`mod`\s3≡2

_test.fmtdecimal.0	.closure	_test.fmtdecimal.0_c
_test.fmtdecimal.0_label	.string	fmtdecimal\s0
_test.fmtdecimal.0_test	.closure	_test.fmtdecimal.0_test_c
_test.fmtdecimal.0_test_expected	.string	0

_test.fmtdecimal.11	.closure	_test.fmtdecimal.11_c
_test.fmtdecimal.11_label	.string	fmtdecimal\s11
_test.fmtdecimal.11_test_expected	.string	11

	.code

_epcode	.expr
test.all	.gvar
_tests	.gvar
	.app	_tests
	.enter	test.all

-- §section §gs{4 ≡ 4}

_test.4.eq.4_c	.expr
_test.4.eq.4_test_c	.subcode
test.property.label	.gvar
_test.4.eq.4_label	.gvar
test	.closure	_test.4.eq.4_test_c
	.app	_test.4.eq.4_label	test
	.enter	test.property.label

_test.4.eq.4_test_c	.expr
_test.4.eq.4_test_eq_c	.subcode
test.assert	.gvar
_test.4.eq.4_test_fail	.gvar
_test.4.eq.4_test_succ	.gvar
eq	.closure	_test.4.eq.4_test_eq_c
	.app	_test.4.eq.4_test_fail	_test.4.eq.4_test_succ	eq
	.enter	test.assert

_test.4.eq.4_test_eq_c	.expr
natural.≡	.gvar
natural.4	.gvar
	.app	natural.4	natural.4
	.enter	natural.≡

-- §section §gs{4 ≡ 0}

_test.4.eq.0_c	.expr
_test.4.eq.0_test_c	.subcode
test.property.label	.gvar
_test.4.eq.0_label	.gvar
test	.closure	_test.4.eq.0_test_c
	.app	_test.4.eq.0_label	test
	.enter	test.property.label

_test.4.eq.0_test_c	.expr
_test.4.eq.0_test_eq_c	.subcode
test.deny	.gvar
_test.4.eq.0_test_succ	.gvar
_test.4.eq.0_test_fail	.gvar
eq	.closure	_test.4.eq.0_test_eq_c
	.app	_test.4.eq.0_test_succ	_test.4.eq.0_test_fail	eq
	.enter	test.deny

_test.4.eq.0_test_eq_c	.expr
natural.≡	.gvar
natural.4	.gvar
natural.0	.gvar
	.app	natural.4	natural.0
	.enter	natural.≡

-- §section §gs{0 < 4}

_test.0.lt.4_c	.expr
test.property.label	.gvar
_test.0.lt.4_label	.gvar
_test.0.lt.4_test	.gvar
	.app	_test.0.lt.4_label	_test.0.lt.4_test
	.enter	test.property.label

_test.0.lt.4_test_c	.expr
_test.0.lt.4_test_lt_c	.subcode
test.assert	.gvar
_test.0.lt.4_test_fail	.gvar
_test.0.lt.4_test_succ	.gvar
lt	.closure	_test.0.lt.4_test_lt_c
	.app	_test.0.lt.4_test_fail	_test.0.lt.4_test_succ	lt
	.enter	test.assert

_test.0.lt.4_test_lt_c	.expr
natural.<	.gvar
natural.0	.gvar
natural.4	.gvar
	.app	natural.0	natural.4
	.enter	natural.<

-- §section §gs{4 < 0}

_test.4.lt.0_c	.expr
test.property.label	.gvar
_test.4.lt.0_label	.gvar
_test.4.lt.0_test	.gvar
	.app	_test.4.lt.0_label	_test.4.lt.0_test
	.enter	test.property.label

_test.4.lt.0_test_c	.expr
_test.4.lt.0_test_lt_c	.subcode
test.deny	.gvar
_test.4.lt.0_test_succ	.gvar
_test.4.lt.0_test_fail	.gvar
lt	.closure	_test.4.lt.0_test_lt_c
	.app	_test.4.lt.0_test_succ	_test.4.lt.0_test_fail	lt
	.enter	test.deny

_test.4.lt.0_test_lt_c	.expr
natural.<	.gvar
natural.4	.gvar
natural.0	.gvar
	.app	natural.4	natural.0
	.enter	natural.<

-- §section §gs{4 > 0}

_test.4.gt.0_c	.expr
test.property.label	.gvar
_test.4.gt.0_label	.gvar
test.assert	.gvar
_test.4.gt.0_test_false	.gvar
_test.4.gt.0_test_true	.gvar
natural.>	.gvar
natural.4	.gvar
natural.0	.gvar
gt	.apply	natural.>	|	natural.4	natural.0
test	.apply	test.assert	|	_test.4.gt.0_test_false	_test.4.gt.0_test_true	gt
	.app	_test.4.gt.0_label	test
	.enter	test.property.label

-- §section §gs{compare 4 4}

_test.compare.4.4_c	.expr
order.total.ord.t	.tygvar
test.property.label	.gvar
_test.compare.4.4_label	.gvar
test.assert.eq	.gvar
order.total.ord.≡	.gvar
order.total.ord.fmtgs	.gvar
natural.compare	.gvar
natural.4	.gvar
order.total.ord.eq	.gvar
actual	.apply	natural.compare	|	natural.4	natural.4
test	.apply	test.assert.eq	order.total.ord.t	|	order.total.ord.≡	order.total.ord.fmtgs	actual	order.total.ord.eq
	.app	_test.compare.4.4_label	test
	.enter	test.property.label

-- §section §gs{2 + 3}

_test_2.plus.3_c	.expr
_test_2.plus.3_test_c	.subcode
test.property.label	.gvar
_test_2.plus.3_label	.gvar
test	.closure	_test_2.plus.3_test_c
	.app	_test_2.plus.3_label	test
	.enter	test.property.label

_test_2.plus.3_test_c	.expr
natural.t	.tygvar
_test_2.plus.3_test_actual_c	.subcode
test.assert.eq	.gvar
natural.≡	.gvar
natural.fmtgs	.gvar
natural.5	.gvar
actual	.closure	_test_2.plus.3_test_actual_c
	.app	natural.≡	natural.fmtgs	actual	natural.5
	.enter	test.assert.eq	natural.t

_test_2.plus.3_test_actual_c	.expr
natural.+	.gvar
natural.2	.gvar
natural.3	.gvar
	.app	natural.2	natural.3
	.enter	natural.+

-- §section §gs{1 `divMod` 0}

_test.1.div.0_c	.expr
test.property.label	.gvar
_test.1.div.0_label	.gvar
_test.1.div.0_test	.gvar
	.app	_test.1.div.0_label	_test.1.div.0_test
	.enter	test.property.label

_test.1.div.0_test_c	.expr
_test.1.div.0_test_k0	.subcode
_test.1.div.0_test_k1	.subcode
natural.prim.divMod	.gvar
1	.natural	1
0	.natural	0
	.ubanalyze	0	_test.1.div.0_test_k0	1	_test.1.div.0_test_k1
	.app	1	0
	.yield	natural.prim.divMod

_test.1.div.0_test_k0	.ubcasecont
test.property.true	.gvar
_test.1.div.0_test_succ	.gvar
	.app	_test.1.div.0_test_succ
	.enter	test.property.true

_test.1.div.0_test_k1	.ubcasecont
natural.prim.u	.tygvar
test.property.false	.gvar
_test.1.div.0_test_fail	.gvar
div	.fkarg	0	natural.prim.u
mod	.fkarg	1	natural.prim.u
	.app	_test.1.div.0_test_fail
	.enter	test.property.false

-- §section §gs{2 `divMod` 3}

_test.2.div.3_c	.expr
test.property.label	.gvar
_test.2.div.3_label	.gvar
_test.2.div.3_test	.gvar
	.app	_test.2.div.3_label	_test.2.div.3_test
	.enter	test.property.label

_test.2.div.3_test_c	.expr
_test.2.div.3_test_k	.subcode
natural.divMod	.gvar
natural.2	.gvar
natural.3	.gvar
	.force	_test.2.div.3_test_k
	.app	natural.2	natural.3
	.enter	natural.divMod

_test.2.div.3_test_k	.forcecont
_test.2.div.3_test_qm0_ty	.tygvar
_test.2.div.3_test_quot_c	.subcode
_test.2.div.3_test_mod_c	.subcode
test.property.∧	.gvar
qm0	.karg	_test.2.div.3_test_qm0_ty
q	.field	0	qm0
m	.field	1	qm0
quot	.closure	_test.2.div.3_test_quot_c
mod	.closure	_test.2.div.3_test_mod_c
	.app	quot	mod
	.enter	test.property.∧

_test.2.div.3_test_quot_c	.expr
natural.t	.tygvar
_test.2.div.3_test_quot_eq_c	.subcode
test.assert	.gvar
_test.2.div.3_test_quot_fail	.gvar
_test.2.div.3_test_quot_succ	.gvar
q	.fv	natural.t
eq	.closure	_test.2.div.3_test_quot_eq_c
	.app	_test.2.div.3_test_quot_fail	_test.2.div.3_test_quot_succ	eq
	.enter	test.assert

_test.2.div.3_test_quot_eq_c	.expr
natural.t	.tygvar
natural.≡	.gvar
natural.0	.gvar
q	.fv	natural.t
	.app	q	natural.0
	.enter	natural.≡

_test.2.div.3_test_mod_c	.expr
natural.t	.tygvar
_test.2.div.3_test_mod_eq_c	.subcode
test.assert	.gvar
_test.2.div.3_test_mod_fail	.gvar
_test.2.div.3_test_mod_succ	.gvar
m	.fv	natural.t
eq	.closure	_test.2.div.3_test_mod_eq_c
	.app	_test.2.div.3_test_mod_fail	_test.2.div.3_test_mod_succ	eq
	.enter	test.assert

_test.2.div.3_test_mod_eq_c	.expr
natural.t	.tygvar
natural.≡	.gvar
natural.2	.gvar
m	.fv	natural.t
	.app	m	natural.2
	.enter	natural.≡

-- §section §gs{natural.fmtdecimal 0}

_test.fmtdecimal.0_c	.expr
test.property.label	.gvar
_test.fmtdecimal.0_label	.gvar
_test.fmtdecimal.0_test	.gvar
	.app	_test.fmtdecimal.0_label	_test.fmtdecimal.0_test
	.enter	test.property.label

_test.fmtdecimal.0_test_c	.expr
string.t	.tygvar
_test.fmtdecimal.0_test_0.s_c	.subcode
test.assert.eq	.gvar
string.≡	.gvar
string.fmtgs	.gvar
_test.fmtdecimal.0_test_expected	.gvar
0.s	.closure	_test.fmtdecimal.0_test_0.s_c
	.app	string.≡ string.fmtgs	0.s	_test.fmtdecimal.0_test_expected
	.enter	test.assert.eq	string.t

_test.fmtdecimal.0_test_0.s_c	.expr
natural.fmtdecimal	.gvar
natural.0	.gvar
	.app	natural.0
	.enter	natural.fmtdecimal

_test.fmtdecimal.11_c	.expr
_test.fmtdecimal.11_test_c	.subcode
test.property.label	.gvar
_test.fmtdecimal.11_label	.gvar
test	.closure	_test.fmtdecimal.11_test_c
	.app	_test.fmtdecimal.11_label	test
	.enter	test.property.label

_test.fmtdecimal.11_test_c	.expr
string.t	.tygvar
_test.fmtdecimal.11_test_11.s_c	.subcode
test.assert.eq	.gvar
string.≡	.gvar
string.fmtgs	.gvar
_test.fmtdecimal.11_test_expected	.gvar
11.s	.closure	_test.fmtdecimal.11_test_11.s_c
	.app	string.≡	string.fmtgs	11.s	_test.fmtdecimal.11_test_expected
	.enter	test.assert.eq	string.t

_test.fmtdecimal.11_test_11.s_c	.expr
natural.fmtdecimal	.gvar
natural.11	.gvar
	.app	natural.11
	.enter	natural.fmtdecimal

	.type

_test.2.div.3_test_qm0_ty	.tyexpr
natural.t	.tygvar
	.typroduct	0	natural.t	1	natural.t
